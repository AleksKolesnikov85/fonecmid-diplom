#Область СлужебныеПроцедурыИФункции

Процедура ОтправкаСообщенийВТелеграм() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ВКМ_ОтправкаУведомленийВТелеграм);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL;
	
	Соединение = Новый HTTPСоединение("api.telegram.org", 443, , , , , ЗащищенноеСоединение);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("content-type", "application/json");
	
	TokenTelegram = Константы.ВКМ_ТокенУправленияТелеграмБотом.Получить();
	IDГруппы = Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить();
	Ресурс = "bot" + TokenTelegram + "/sendMessage";
	
	HTTPЗапрос = Новый HTTPЗапрос(Ресурс, Заголовки);
	
	Данные = Новый Структура;
	Данные.Вставить("chat_id", IDГруппы);
	
	ЗапросСообщений = Новый Запрос;
	ЗапросСообщений.Текст = "ВЫБРАТЬ
	|	ВКМ_УведомленияТелеграмБоту.ТекстСообщения КАК ТекстСообщения,
	|	ВКМ_УведомленияТелеграмБоту.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту
	|ГДЕ
	|	НЕ ВКМ_УведомленияТелеграмБоту.ПометкаУдаления";
	
	Выборка = ЗапросСообщений.Выполнить().Выбрать();
		
	Пока Выборка.Следующий() Цикл
	
		Данные.Вставить("text", Выборка.ТекстСообщения);
		
		ЗаписьJSON = Новый ЗаписьJSON();
		ЗаписьJSON.УстановитьСтроку();
		ЗаписатьJSON(ЗаписьJSON, Данные);
		СтрокаЗапроса = ЗаписьJSON.Закрыть();
		
		HTTPЗапрос.УстановитьТелоИзСтроки(СтрокаЗапроса);
		
		HTTPОтвет = Соединение.Получить(HTTPЗапрос);
		
		Если HTTPОтвет.КодСостояния = 200 Тогда
			Уведомление = Выборка.Ссылка.ПолучитьОбъект();
			Уведомление.Удалить();
		Иначе
			ТекстОшибки = HTTPОтвет.ПолучитьТелоКакСтроку();
			ЗаписьЖурналаРегистрации("Ошибка отправки ботом сообщений в телеграм", УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти