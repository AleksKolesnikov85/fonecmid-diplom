
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	ОбновитьСписокНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	ОбновитьСписокНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СформироватьАкты(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Период) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не выбран период", , "Объект.Период");
		Возврат;	
	КонецЕсли;
	
	ПараметрыЗапуска = Новый Структура;
	ПараметрыЗапуска.Вставить("ДатаНачала", Объект.Период.ДатаНачала);
	ПараметрыЗапуска.Вставить("ДатаОкончания", Объект.Период.ДатаОкончания);
	
	ДлительнаяОперация = СформироватьАктыНаСервере(ПараметрыЗапуска, УникальныйИдентификатор);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Интервал = 1;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	
	ОписаниеОЗавершении = Новый ОписаниеОповещения("ОписаниеОЗавершении", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОписаниеОЗавершении(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Статус = "Выполнено" Тогда
		ТекстСообщения = "Выполнено";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
		ОбновитьСписокНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура ОбновитьСписокНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.Период) Тогда
		ОбщегоНазначения.СообщитьПользователю("Не выбран период", , "Объект.Период");
		Возврат;	
	КонецЕсли;
	
	Объект.СписокДоговоров.Очистить();
	
	Выборка = Обработки.ВКМ_МассовоеСозданиеАктов.ПолучитьДанныеДоговоров(Объект.Период.ДатаНачала, Объект.Период.ДатаОкончания);
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.СписокДоговоров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция СформироватьАктыНаСервере(ПараметрыЗапуска, УникальныйИдентификатор)
	
	ИмяПроцедуры = "Обработки.ВКМ_МассовоеСозданиеАктов.СформироватьРеализации";
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьПроцедуру(УникальныйИдентификатор, ИмяПроцедуры, ПараметрыЗапуска.ДатаНачала, ПараметрыЗапуска.ДатаОкончания);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

#КонецОбласти
