
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// регистр ВКМ_ВзаиморасчетыССотрудниками
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	
	Если Выплаты.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю("Табличная часть не заполнена");
		Отказ = Истина;
	КонецЕсли;
	
	Для Каждого ТекСтрокаВыплаты Из Выплаты Цикл
		
		Если Не ЗначениеЗаполнено(ТекСтрокаВыплаты.СуммаВыплаты)  Тогда
			
			ТекстСообщения = СтрШаблон("Не указана сумма выплаты для сотрудника %1", ТекСтрокаВыплаты.Сотрудник);
			Поле = СтрШаблон("Объект.Выплаты[%1].СуммаВыплаты", Выплаты.Индекс(ТекСтрокаВыплаты));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , Поле);
			
			Отказ = Истина;
			Продолжить;
			
		КонецЕсли;
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Подразделение = Подразделение;
		Движение.Сотрудник = ТекСтрокаВыплаты.Сотрудник;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Выплата;
		Движение.Сумма = ТекСтрокаВыплаты.СуммаВыплаты;
		
	КонецЦикла;

	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();

КонецПроцедуры

#КонецОбласти

#КонецЕсли