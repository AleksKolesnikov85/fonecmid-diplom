
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Данные = ПолучитьИзВременногоХранилища(Параметры.Адрес);
	ГодДокумента = Параметры.ГодДокумента;
	
	Для Каждого Строка Из Данные Цикл
		НоваяСтрока = ДанныеДиаграммы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;
	
	Год = ГодДокумента;
	
	ПостроитьДиаграмму(Данные, ГодДокумента);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Уменьшить(Команда)
	
	Данные = ДанныеДиаграммы;
	ГодДокумента = Год;
	
	ПостроитьДиаграмму(Данные, ГодДокумента);
	
	Элементы.ФормаУменьшить.Доступность = Ложь;
	Элементы.ФормаУвеличить.Доступность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура Увеличить(Команда)
	
	Данные = ДанныеДиаграммы;
	ГодДокумента = Год;
	
	ПостроитьПодробнуюДиаграмму(Данные, ГодДокумента);
	
	Элементы.ФормаУменьшить.Доступность = Истина;
	Элементы.ФормаУвеличить.Доступность = Ложь;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПостроитьДиаграмму(Данные, ГодДокумента)
	
	АнализГрафика.Обновление = Ложь;
	АнализГрафика.Очистить();
	АнализГрафика.ПоддержкаМасштаба = ПоддержкаМасштабаДиаграммыГанта.ВсеДанные;
	АнализГрафика.АвтоОпределениеПолногоИнтервала = Ложь;
	АнализГрафика.УстановитьПолныйИнтервал(НачалоГода(ГодДокумента), КонецГода(ГодДокумента));
	
	Шкала = АнализГрафика.ОбластьПостроения.ШкалаВремени.Элементы;
	Если Шкала.Количество() > 1 Тогда
		Шкала.Удалить(АнализГрафика.ОбластьПостроения.ШкалаВремени.Элементы[1]);
	КонецЕсли;
		
	Для Каждого Строка Из Данные Цикл
		
		Точка = АнализГрафика.УстановитьТочку(Строка.Сотрудник);
		Точка.Текст = Строка.Сотрудник;
		
		Серия = АнализГрафика.УстановитьСерию(ГодДокумента);
		Серия.Текст = Формат(ГодДокумента, "ДФ=yyyy;");
		
		Значение = АнализГрафика.ПолучитьЗначение(Точка, Серия);
		
		Интервал = Значение.Добавить();
		Интервал.Начало = Строка.ДатаНачала;
		Интервал.Конец = Строка.ДатаОкончания;
		
	КонецЦикла;
	
	АнализГрафика.Обновление = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПостроитьПодробнуюДиаграмму(ДанныеДиаграммы, Год)
	
	АнализГрафика.Обновление = Ложь;
	АнализГрафика.Очистить();
	АнализГрафика.ПоддержкаМасштаба = ПоддержкаМасштабаДиаграммыГанта.Период;
	
	Шкала = АнализГрафика.ОбластьПостроения.ШкалаВремени.Элементы;
	ШкалаДень = Шкала.Добавить();
	ШкалаДень.Единица = ТипЕдиницыШкалыВремени.День;
	ШкалаДень.ФорматДня = ФорматДняШкалыВремени.ДеньМесяца;
	
	Для Каждого Строка Из ДанныеДиаграммы Цикл
		
		Точка = АнализГрафика.УстановитьТочку(Строка.Сотрудник);
		Точка.Текст = Строка.Сотрудник;
		
		Серия = АнализГрафика.УстановитьСерию(Год);
		Серия.Текст = Формат(Год, "ДФ=yyyy;");
		
		Значение = АнализГрафика.ПолучитьЗначение(Точка, Серия);
		
		Интервал = Значение.Добавить();
		Интервал.Начало = Строка.ДатаНачала;
		Интервал.Конец = Строка.ДатаОкончания;
		
	КонецЦикла;
	
	АнализГрафика.Обновление = Истина;
	
КонецПроцедуры

#КонецОбласти

