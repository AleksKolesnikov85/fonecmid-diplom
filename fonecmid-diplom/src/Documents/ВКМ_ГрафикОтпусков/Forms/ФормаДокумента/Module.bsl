
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
//	ПроверитьПревышениеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	ПроверитьПревышениеНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтпускаСотрудников

&НаКлиенте
Процедура ОтпускаСотрудниковПриИзменении(Элемент)
	ОтпускаСотрудниковПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура АнализГрафика(Команда)
	
	АдресДанных = АнализГрафикаНаСервере();
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Адрес", АдресДанных);
	СтруктураПараметров.Вставить("ГодДокумента", Объект.Год);
	
	ОткрытьФорму("Документ.ВКМ_ГрафикОтпусков.Форма.АнализГрафика", СтруктураПараметров, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция АнализГрафикаНаСервере()
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ТаблицаОтпусков = ДокументОбъект.ОтпускаСотрудников.Выгрузить();
	
	Адрес = ПоместитьВоВременноеХранилище(ТаблицаОтпусков, УникальныйИдентификатор);
	
	Возврат Адрес;
	
КонецФункции

&НаСервере
Процедура ПроверитьПревышениеНаСервере()
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ТаблицаОтпусков = ДокументОбъект.ОтпускаСотрудников.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Таблица", ТаблицаОтпусков);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Сотрудник КАК Сотрудник,
	|	Таблица.НомерСтроки КАК НомерСтроки,
	|	РАЗНОСТЬДАТ(Таблица.ДатаНачала, Таблица.ДатаОкончания, ДЕНЬ) + 1 КАК КоличествоДнейОтпуска
	|ПОМЕСТИТЬ ВТ_ТаблицаОтпусков
	|ИЗ
	|	&Таблица КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаОтпусков.Сотрудник КАК ВыбранныйСотрудник,
	|	СУММА(ВТ_ТаблицаОтпусков.КоличествоДнейОтпуска) КАК ОбщееКоличествоДней
	|ПОМЕСТИТЬ ВТ_ТаблицаОтпусков2
	|ИЗ
	|	ВТ_ТаблицаОтпусков КАК ВТ_ТаблицаОтпусков
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаОтпусков.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаОтпусков.Сотрудник КАК Сотрудник,
	|	ВТ_ТаблицаОтпусков.НомерСтроки КАК НомерСтроки,
	|	ВТ_ТаблицаОтпусков2.ОбщееКоличествоДней КАК ОбщееКоличествоДней
	|ИЗ
	|	ВТ_ТаблицаОтпусков КАК ВТ_ТаблицаОтпусков
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаОтпусков2 КАК ВТ_ТаблицаОтпусков2
	|		ПО ВТ_ТаблицаОтпусков.Сотрудник = ВТ_ТаблицаОтпусков2.ВыбранныйСотрудник";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Индекс = Выборка.НомерСтроки - 1;
		ТаблицаОтпусков[Индекс].ОбщееКоличествоДней = Выборка.ОбщееКоличествоДней;
	КонецЦикла;
	
	ДокументОбъект.ОтпускаСотрудников.Загрузить(ТаблицаОтпусков);
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура ОтпускаСотрудниковПриИзмененииНаСервере()
	ПроверитьПревышениеНаСервере();
КонецПроцедуры

#КонецОбласти