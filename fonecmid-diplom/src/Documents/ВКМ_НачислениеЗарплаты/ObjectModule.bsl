#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ВКМ_ОсновныеНачисления.Записывать = Истина;
	Движения.ВКМ_Удержания.Записывать = Истина;
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	Движения.ВКМ_ОтпускаСотрудников.Записывать = Истина;
	
	ОтменитьПроведение = СформироватьДвижения();
	
	Если ОтменитьПроведение Тогда
		Отказ = Истина;
		Возврат; 
	КонецЕсли;
	
	СформироватьСторноЗаписи();
	
	Движения.ВКМ_ОсновныеНачисления.Записать();
	
	СформироватьДвиженияУдержания();
	
	РассчитатьОклад();
	РассчитатьОтпуск();
	РассчитатьУдержания();
	
	СформироватьДвиженияПоВзаиморасчетам();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьДвижения()
	
	ОтменитьПроведение = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_НачислениеЗарплатыОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ВКМ_НачислениеЗарплатыОсновныеНачисления.Подразделение КАК Подразделение,
		|	ВКМ_НачислениеЗарплатыОсновныеНачисления.ВидРасчета КАК ВидРасчета,
		|	ВКМ_НачислениеЗарплатыОсновныеНачисления.ГрафикРаботы КАК ГрафикРаботы,
		|	НАЧАЛОПЕРИОДА(ВКМ_НачислениеЗарплатыОсновныеНачисления.ДатаНачала, ДЕНЬ) КАК ПериодДействияНачало,
		|	КОНЕЦПЕРИОДА(ВКМ_НачислениеЗарплатыОсновныеНачисления.ДатаОкончания, ДЕНЬ) КАК ПериодДействияКонец,
		|	ВЫБОР
		|		КОГДА ВКМ_НачислениеЗарплатыОсновныеНачисления.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск)
		|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВКМ_НачислениеЗарплатыОсновныеНачисления.ДатаНачала, МЕСЯЦ, -12), МЕСЯЦ)
		|	КОНЕЦ КАК БазовыйПериодНачало,
		|	ВЫБОР
		|		КОГДА ВКМ_НачислениеЗарплатыОсновныеНачисления.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск)
		|			ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(ВКМ_НачислениеЗарплатыОсновныеНачисления.ДатаОкончания, МЕСЯЦ, -1), МЕСЯЦ)
		|	КОНЕЦ КАК БазовыйПериодКонец
		|ПОМЕСТИТЬ ВТ_Док
		|ИЗ
		|	Документ.ВКМ_НачислениеЗарплаты.ОсновныеНачисления КАК ВКМ_НачислениеЗарплатыОсновныеНачисления
		|ГДЕ
		|	ВКМ_НачислениеЗарплатыОсновныеНачисления.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Док.Сотрудник КАК Сотрудник,
		|	ВТ_Док.Подразделение КАК Подразделение,
		|	ВТ_Док.ВидРасчета КАК ВидРасчета,
		|	ВТ_Док.ГрафикРаботы КАК ГрафикРаботы,
		|	ВТ_Док.ПериодДействияНачало КАК ПериодДействияНачало,
		|	ВТ_Док.ПериодДействияКонец КАК ПериодДействияКонец,
		|	ВТ_Док.БазовыйПериодНачало КАК БазовыйПериодНачало,
		|	ВТ_Док.БазовыйПериодКонец КАК БазовыйПериодКонец,
		|	ЕСТЬNULL(ВКМ_УсловияОплатыСотрудниковСрезПоследних.Оклад, 0) КАК Показатель,
		|	&Период КАК ПериодРегистрации
		|ИЗ
		|	ВТ_Док КАК ВТ_Док
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Период, Сотрудник В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ВТ_Док.Сотрудник КАК Сотрудник
		|			ИЗ
		|				ВТ_Док КАК ВТ_Док)) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
		|		ПО ВТ_Док.Сотрудник = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Показатель = 0 Тогда 
			
			ТекстСообщения = СтрШаблон("Для сотрудника : %1 не установлен оклад в расчетном периоде", Выборка.Сотрудник);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			
			ОтменитьПроведение = Истина;
			
		КонецЕсли;
		
		Если ОтменитьПроведение Тогда
			Продолжить;
		КонецЕсли;
		
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);

	КонецЦикла;
	
	Возврат ОтменитьПроведение;
	
КонецФункции

Процедура СформироватьСторноЗаписи()
	
	СторноЗаписи = Движения.ВКМ_ОсновныеНачисления.ПолучитьДополнение();
	
	Для Каждого Строка Из СторноЗаписи Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Строка);
		
		Движение.ПериодРегистрации = Дата;
		Движение.ПериодДействияНачало = Строка.ПериодДействияНачалоСторно;
		Движение.ПериодДействияКонец = Строка.ПериодДействияКонецСторно;
		Движение.Сторно = Истина;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьДвиженияУдержания()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисления.Подразделение КАК Подразделение,
		|	ВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ВКМ_ОсновныеНачисления.ПериодДействияНачало КАК БазовыйПериодНачало,
		|	ВКМ_ОсновныеНачисления.ПериодДействияКонец КАК БазовыйПериодКонец,
		|	ВКМ_ОсновныеНачисления.Сторно КАК Сторно
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
		|ГДЕ
		|	ВКМ_ОсновныеНачисления.Регистратор = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_ОсновныеНачисления.Сотрудник,
		|	ВКМ_ОсновныеНачисления.ПериодДействияНачало,
		|	ВКМ_ОсновныеНачисления.ПериодДействияКонец,
		|	ВКМ_ОсновныеНачисления.Сторно,
		|	ВКМ_ОсновныеНачисления.Подразделение";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_Удержания.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		
	КонецЦикла;
	
	Движения.ВКМ_Удержания.Записать();
	
КонецПроцедуры

Процедура РассчитатьОклад()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеВДняхПериодДействия КАК План,
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеВДняхФактическийПериодДействия КАК Факт,
		|	ЕСТЬNULL(ВКМ_ВыполненныеСотрудникомРаботыОбороты.СуммаКОплатеОборот, 0) КАК СуммаКОплате
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(Регистратор = &Ссылка
		|	И ВидРасчета = &Оклад) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(&НачалоПериода, &КонецПериода,,) КАК
		|			ВКМ_ВыполненныеСотрудникомРаботыОбороты
		|		ПО ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник = ВКМ_ВыполненныеСотрудникомРаботыОбороты.Сотрудник";
	
	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл

		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки - 1];
		Движение.Результат = (Движение.Показатель * Выборка.Факт / Выборка.План) + Выборка.СуммаКОплате;
		Движение.ОтработаноДней = Выборка.Факт;
		
		Если Движение.Сторно Тогда
			Движение.Результат = -Движение.Результат;
			Движение.ОтработаноДней = -Движение.ОтработаноДней;
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать();
	
КонецПроцедуры

Процедура РассчитатьОтпуск()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ВКМ_ОсновныеНачисления.Подразделение КАК Подразделение,
		|	ВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.РезультатБаза, 0) КАК БазаОсн,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ОтработаноДнейБаза, 0) КАК БазаОтработано,
		|	ВКМ_ОсновныеНачисления.ПериодДействияНачало КАК ДатаНачалаОтпуска,
		|	ВКМ_ОсновныеНачисления.ПериодДействияКонец КАК ДатаОкончанияОтпуска,
		|	ВКМ_ОсновныеНачисления.ГрафикРаботы КАК ГрафикРаботы
		|ПОМЕСТИТЬ ВТ_Данные
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления(&Измерения, &Измерения,,
		|			Регистратор = &Ссылка
		|		И ВидРасчета = &Отпуск) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления
		|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.НомерСтроки
		|ГДЕ
		|	ВКМ_ОсновныеНачисления.Регистратор = &Ссылка
		|	И ВКМ_ОсновныеНачисления.ВидРасчета = &Отпуск
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Данные.НомерСтроки КАК НомерСтроки,
		|	СУММА(ВКМ_ГрафикиРаботы.ЗначениеВДнях) КАК КоличествоДнейОтпуска
		|ПОМЕСТИТЬ ВТ_ДниОтпуска
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_ГрафикиРаботы КАК ВКМ_ГрафикиРаботы
		|		ПО ВКМ_ГрафикиРаботы.Дата МЕЖДУ ВТ_Данные.ДатаНачалаОтпуска И ВТ_Данные.ДатаОкончанияОтпуска
		|ГДЕ
		|	ВКМ_ГрафикиРаботы.ГрафикРаботы = ВТ_Данные.ГрафикРаботы
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Данные.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Данные.НомерСтроки КАК НомерСтроки,
		|	ВТ_Данные.Подразделение КАК Подразделение,
		|	ВТ_Данные.Сотрудник КАК Сотрудник,
		|	ВТ_Данные.БазаОсн КАК БазаОсн,
		|	ВТ_Данные.БазаОтработано КАК БазаОтработано,
		|	ВТ_Данные.ДатаНачалаОтпуска КАК ДатаНачала,
		|	ВТ_Данные.ДатаОкончанияОтпуска КАК ДатаОкончания,
		|	ВТ_ДниОтпуска.КоличествоДнейОтпуска КАК КоличествоДнейОтпуска
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДниОтпуска КАК ВТ_ДниОтпуска
		|		ПО ВТ_Данные.НомерСтроки = ВТ_ДниОтпуска.НомерСтроки";
	
	Запрос.УстановитьПараметр("Отпуск", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	Измерения.Добавить("Подразделение");
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки - 1];
		
		Движение.ОтработаноДней = Выборка.КоличествоДнейОтпуска;
		
		Если Выборка.БазаОтработано = 0 Тогда
			Движение.Результат = 0;
			Продолжить;
		КонецЕсли;
		
		Движение.Результат = (Выборка.БазаОсн / Выборка.БазаОтработано) * Выборка.КоличествоДнейОтпуска;
		
		Движение = Движения.ВКМ_ОтпускаСотрудников.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		Движение.Период = Выборка.ДатаНачала;
		Движение.Год = Выборка.ДатаНачала;
		
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать();
	Движения.ВКМ_ОтпускаСотрудников.Записать();
	
КонецПроцедуры

Процедура РассчитатьУдержания()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_Удержания.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.РезультатБаза, 0) КАК РезультатБаза
		|ИЗ
		|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_Удержания.БазаВКМ_ОсновныеНачисления(&Измерения, &Измерения,,
		|			Регистратор = &Регистратор
		|		И ВидРасчета = &НДФЛ) КАК ВКМ_УдержанияБазаВКМ_ОсновныеНачисления
		|		ПО ВКМ_Удержания.Сотрудник = ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.Сотрудник
		|		И ВКМ_Удержания.Подразделение = ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.Подразделение
		|		И ВКМ_Удержания.НомерСтроки = ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.НомерСтроки
		|ГДЕ
		|	ВКМ_Удержания.Регистратор = &Регистратор
		|	И ВКМ_Удержания.ВидРасчета = &НДФЛ";
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	Измерения.Добавить("Подразделение");
	
	Запрос.УстановитьПараметр("НДФЛ", ПланыВидовРасчета.ВКМ_Удержания.НДФЛ);
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	НДФЛ = Константы.ВКМ_НДФЛ.Получить();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_Удержания[Выборка.НомерСтроки - 1];
		
		Движение.Результат = Выборка.РезультатБаза * НДФЛ / 100;
		
	КонецЦикла;
	
	Движения.ВКМ_Удержания.Записать();
	
КонецПроцедуры

Процедура СформироватьДвиженияПоВзаиморасчетам()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисления.Подразделение КАК Подразделение,
		|	ВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ВКМ_ОсновныеНачисления.ВидРасчета КАК ВидРасчета,
		|	ВКМ_ОсновныеНачисления.Результат КАК Результат
		|ПОМЕСТИТЬ ВТ_Начисления
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
		|ГДЕ
		|	ВКМ_ОсновныеНачисления.Регистратор = &Регистратор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВКМ_Удержания.Подразделение,
		|	ВКМ_Удержания.Сотрудник,
		|	ВКМ_Удержания.ВидРасчета,
		|	-ВКМ_Удержания.Результат
		|ИЗ
		|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
		|ГДЕ
		|	ВКМ_Удержания.Регистратор = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Начисления.Подразделение КАК Подразделение,
		|	ВТ_Начисления.Сотрудник КАК Сотрудник,
		|	ВТ_Начисления.ВидРасчета КАК ВидРасчета,
		|	СУММА(ВТ_Начисления.Результат) КАК Сумма
		|ИЗ
		|	ВТ_Начисления КАК ВТ_Начисления
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Начисления.Сотрудник,
		|	ВТ_Начисления.ВидРасчета,
		|	ВТ_Начисления.Подразделение";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьПриход();
		Движение.Период = Дата;
		ЗаполнитьЗначенияСвойств(Движение, Выборка);

	КонецЦикла;
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли