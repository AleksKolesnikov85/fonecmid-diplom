#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ВКМ_ДополнительныеНачисления.Записывать = Истина;
	Движения.ВКМ_Удержания.Записывать = Истина;
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	
	СформироватьДвиженияДопНачисления();
	
	СформироватьДвиженияУдержания();
	РассчитатьУдержания();
	
	СформироватьДвиженияПоВзаиморасчетам();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвиженияДопНачисления()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_НачислениеФиксированныхПремийСписокСотрудников.Подразделение КАК Подразделение,
		|	ВКМ_НачислениеФиксированныхПремийСписокСотрудников.Сотрудник КАК Сотрудник,
		|	ВКМ_НачислениеФиксированныхПремийСписокСотрудников.СуммаПремии КАК Результат,
		|	ВКМ_НачислениеФиксированныхПремийСписокСотрудников.Ссылка.Дата КАК ПериодРегистрации
		|ИЗ
		|	Документ.ВКМ_НачислениеФиксированныхПремий.СписокСотрудников КАК ВКМ_НачислениеФиксированныхПремийСписокСотрудников
		|ГДЕ
		|	ВКМ_НачислениеФиксированныхПремийСписокСотрудников.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.Премия;
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
		
	КонецЦикла;
	
	Движения.ВКМ_ДополнительныеНачисления.Записать();
	
КонецПроцедуры

Процедура СформироватьДвиженияУдержания()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ДополнительныеНачисления.Подразделение КАК Подразделение,
		|	ВКМ_ДополнительныеНачисления.Сотрудник КАК Сотрудник,
		|	НАЧАЛОПЕРИОДА(ВКМ_ДополнительныеНачисления.ПериодРегистрации, МЕСЯЦ) КАК БазовыйПериодНачало,
		|	КОНЕЦПЕРИОДА(ВКМ_ДополнительныеНачисления.ПериодРегистрации, МЕСЯЦ) КАК БазовыйПериодКонец,
		|	ВКМ_ДополнительныеНачисления.Сторно КАК Сторно
		|ИЗ
		|	РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
		|ГДЕ
		|	ВКМ_ДополнительныеНачисления.Регистратор = &Регистратор
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_ДополнительныеНачисления.Сотрудник,
		|	НАЧАЛОПЕРИОДА(ВКМ_ДополнительныеНачисления.ПериодРегистрации, МЕСЯЦ),
		|	КОНЕЦПЕРИОДА(ВКМ_ДополнительныеНачисления.ПериодРегистрации, МЕСЯЦ),
		|	ВКМ_ДополнительныеНачисления.Сторно,
		|	ВКМ_ДополнительныеНачисления.Подразделение";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_Удержания.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		
	КонецЦикла;
	
	Движения.ВКМ_Удержания.Записать();
	
КонецПроцедуры

Процедура РассчитатьУдержания()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_Удержания.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.РезультатБаза, 0) КАК РезультатБаза
		|ИЗ
		|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_Удержания.БазаВКМ_ДополнительныеНачисления(&Измерения, &Измерения,,
		|			Регистратор = &Регистратор
		|		И ВидРасчета = &НДФЛ) КАК ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления
		|		ПО ВКМ_Удержания.Сотрудник = ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.Сотрудник
		|		И ВКМ_Удержания.Подразделение = ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.Подразделение
		|		И ВКМ_Удержания.НомерСтроки = ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.НомерСтроки
		|ГДЕ
		|	ВКМ_Удержания.Регистратор = &Регистратор
		|	И ВКМ_Удержания.ВидРасчета = &НДФЛ";
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	
	Запрос.УстановитьПараметр("НДФЛ", ПланыВидовРасчета.ВКМ_Удержания.НДФЛ);
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	НДФЛ = Константы.ВКМ_НДФЛ.Получить();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_Удержания[Выборка.НомерСтроки - 1];
		
		Движение.Результат = Выборка.РезультатБаза * НДФЛ / 100;
		
	КонецЦикла;
	
	Движения.ВКМ_Удержания.Записать();
	
КонецПроцедуры


Процедура СформироватьДвиженияПоВзаиморасчетам()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ДополнительныеНачисления.Подразделение КАК Подразделение,
		|	ВКМ_ДополнительныеНачисления.Сотрудник КАК Сотрудник,
		|	ВКМ_ДополнительныеНачисления.ВидРасчета КАК ВидРасчета,
		|	ВКМ_ДополнительныеНачисления.Результат КАК Результат
		|ПОМЕСТИТЬ ВТ_Начисления
		|ИЗ
		|	РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
		|ГДЕ
		|	ВКМ_ДополнительныеНачисления.Регистратор = &Регистратор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВКМ_Удержания.Подразделение,
		|	ВКМ_Удержания.Сотрудник,
		|	ВКМ_Удержания.ВидРасчета,
		|	-ВКМ_Удержания.Результат
		|ИЗ
		|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
		|ГДЕ
		|	ВКМ_Удержания.Регистратор = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Начисления.Подразделение КАК Подразделение,
		|	ВТ_Начисления.Сотрудник КАК Сотрудник,
		|	ВТ_Начисления.ВидРасчета КАК ВидРасчета,
		|	СУММА(ВТ_Начисления.Результат) КАК Сумма
		|ИЗ
		|	ВТ_Начисления КАК ВТ_Начисления
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Начисления.Сотрудник,
		|	ВТ_Начисления.ВидРасчета,
		|	ВТ_Начисления.Подразделение";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьПриход();
		Движение.Период = Дата;
		ЗаполнитьЗначенияСвойств(Движение, Выборка);

	КонецЦикла;
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли