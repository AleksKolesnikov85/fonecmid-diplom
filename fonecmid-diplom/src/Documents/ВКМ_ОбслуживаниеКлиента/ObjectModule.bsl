#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Уведомления = Новый Массив;
	
	Если Не ЭтоНовый() Тогда
			
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОбслуживаниеКлиента.Специалист КАК Специалист,
		|	ВКМ_ОбслуживаниеКлиента.ДатаПроведенияРабот КАК ДатаПроведенияРабот,
		|	ВКМ_ОбслуживаниеКлиента.ВремяНачалаРабот КАК ВремяНачалаРабот,
		|	ВКМ_ОбслуживаниеКлиента.ВремяОкончанияРабот КАК ВремяОкончанияРабот
		|ИЗ
		|	Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
		|ГДЕ
		|	ВКМ_ОбслуживаниеКлиента.Ссылка = &Ссылка";

		Запрос.УстановитьПараметр("Ссылка", Ссылка);

		РезультатЗапроса = Запрос.Выполнить();

		Выборка = РезультатЗапроса.Выбрать();

		Если Выборка.Следующий() Тогда
			СпециалистДок = Выборка.Специалист;
			ДатаПроведенияРаботДок = Выборка.ДатаПроведенияРабот;
			ВремяНачалаРаботДок = Выборка.ВремяНачалаРабот;
			ВремяОкончанияРаботДок = Выборка.ВремяОкончанияРабот;
		КонецЕсли;
		
		Если СпециалистДок <> Специалист Тогда
			Уведомление = СтрШаблон("В заявке №%1 выбран %2 в качестве исполнителя", 
				ПрефиксацияОбъектовКлиентСервер.УдалитьЛидирующиеНулиИзНомераОбъекта(Номер), Специалист);
			Уведомления.Добавить(Уведомление);
		КонецЕсли;
		
		Если ДатаПроведенияРаботДок <> ДатаПроведенияРабот Тогда
			Уведомление = СтрШаблон("В заявке №%1 изменилась дата проведения работ на %2", 
				ПрефиксацияОбъектовКлиентСервер.УдалитьЛидирующиеНулиИзНомераОбъекта(Номер), Формат(ДатаПроведенияРабот, "ДЛФ=D;"));
			Уведомления.Добавить(Уведомление);
		КонецЕсли;
		
		Если ВремяНачалаРаботДок <> ВремяНачалаРабот Тогда
			Уведомление = СтрШаблон("В заявке №%1 изменилось время начала работ на %2", 
				ПрефиксацияОбъектовКлиентСервер.УдалитьЛидирующиеНулиИзНомераОбъекта(Номер), Формат(ВремяНачалаРабот, "ДЛФ=T;"));
			Уведомления.Добавить(Уведомление);
		КонецЕсли;
		
		Если ВремяОкончанияРаботДок <> ВремяОкончанияРабот Тогда
			Уведомление = СтрШаблон("В заявке №%1 изменилось время окончания работ на %2", 
				ПрефиксацияОбъектовКлиентСервер.УдалитьЛидирующиеНулиИзНомераОбъекта(Номер), Формат(ВремяОкончанияРабот, "ДЛФ=T"));
			Уведомления.Добавить(Уведомление);
		КонецЕсли;
		
	Иначе
		Уведомление = СтрШаблон("Добавлена новая заявка на обслуживание от %1. Исполнитель: %2", Формат(Дата, "ДЛФ=D;"), Специалист);
		Уведомления.Добавить(Уведомление);
		
	КонецЕсли;
	
	ДобавитьУведомление(Уведомления);

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ,Режим)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.ВидДоговора КАК ВидДоговора,
	|	ДоговорыКонтрагентов.ВКМ_ДатаНачалаДоговора КАК ДатаНачалаДоговора,
	|	ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДоговора КАК ДатаОкончанияДоговора,
	|	ДоговорыКонтрагентов.ВКМ_СтоимостьЧаса КАК СтоимостьЧаса,
	|	ЕСТЬNULL(ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот, -1) КАК ПроцентОтРабот,
	|	ВКМ_ОбслуживаниеКлиента.ВыполненныеРаботы.(
	|		СУММА(ЧасыКОплате) КАК ЧасыКОплате
	|	) КАК ЧасовКОплате
	|ИЗ
	|	Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО (ВКМ_ОбслуживаниеКлиента.Договор = ДоговорыКонтрагентов.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Дата, Сотрудник = &Специалист) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
	|		ПО (ВКМ_ОбслуживаниеКлиента.Специалист = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник)
	|ГДЕ
	|	ВКМ_ОбслуживаниеКлиента.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Специалист", Специалист);
	Запрос.УстановитьПараметр("Дата", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание Тогда
			ТекстСообщения = НСтр("ru = 'Договор не на абонентское обслуживание.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Объект.Договор");
			Отказ = Истина;
		КонецЕсли;
		
		Если Дата < Выборка.ДатаНачалаДоговора ИЛИ Дата > Выборка.ДатаОкончанияДоговора Тогда
			ТекстСообщения = НСтр("ru = 'Дата документа за пределами сроков договора абонентского обслуживания.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Объект.Дата");
			Отказ = Истина;
		КонецЕсли;
		
		Если Выборка.ПроцентОтРабот < 0 Тогда
			ТекстСообщения = НСтр("ru = 'На данный период не задан процент от работ для специалиста.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Объект.Специалист");
			Отказ = Истина;
		КонецЕсли;
		
		СтавкаЧаса = Выборка.СтоимостьЧаса;
		ПроцентОтРабот = Выборка.ПроцентОтРабот;

	КонецЦикла;
	
	// регистр ВКМ_ВыполненныеКлиентуРаботы
	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
	
	Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
	Движение.Период = Дата;
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Клиент = Клиент;
	Движение.Договор = Договор;
	Движение.КоличествоЧасов = ВыполненныеРаботы.Итог("ЧасыКОплате");
	Движение.СуммаКОплате = Движение.КоличествоЧасов * СтавкаЧаса;
	
	// регистр ВКМ_ВыполненныеСотрудникомРаботы
	Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
	
	Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
	Движение.Период = Дата;
	Движение.Сотрудник = Специалист;
	Движение.ЧасовКОплате = ВыполненныеРаботы.Итог("ЧасыКОплате");
	Движение.СуммаКОплате = Движение.ЧасовКОплате * СтавкаЧаса * ПроцентОтРабот / 100;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьУведомление(Уведомления)
	
	Если Уведомления.Количество() <> 0 Тогда
		Для Каждого Уведомление Из Уведомления Цикл
			НовоеУведомление = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
			НовоеУведомление.ТекстСообщения = Уведомление;
			НовоеУведомление.Записать();
		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли
