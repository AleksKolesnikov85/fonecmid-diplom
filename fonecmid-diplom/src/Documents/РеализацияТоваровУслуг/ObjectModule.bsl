
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	СуммаДокумента = Товары.Итог("Сумма") + Услуги.Итог("Сумма");
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ОбработкаЗаказов.Записывать = Истина;
	Движения.ОстаткиТоваров.Записывать = Истина;
	
	// ++ Колесников А.А. ВКМ 30.01.26
	Движения.ВКМ_ОбработкаАктов.Записывать = Истина;
	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
	// -- Колесников А.А. ВКМ 30.01.26
	
	РаботыСпециалиста = Константы.ВКМ_НоменклатураРаботыСпециалиста.Получить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Договор.ВидДоговора КАК ВидДоговора
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|ГДЕ
		|	РеализацияТоваровУслуг.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		// ++ Колесников А.А. ВКМ 30.01.26
		// Дополнительное формирование движения до регистру ВКМ_ОбработкаАктов для упрощения массового создания реализация за период
		// Дополнительное формирование движения до регистру ВКМ_ВыполненныеКлиентуРаботы для формирования отчета выставленных актов
		Если Выборка.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание Тогда
			
			Движение = Движения.ВКМ_ОбработкаАктов.Добавить();
			Движение.Период = Дата;
			Движение.Контрагент = Контрагент;
			Движение.Договор = Договор;
			Движение.Акт = Ссылка;
			Движение.СуммаАкта = СуммаДокумента;
			
			Для Каждого ТекСтрокаУслуги Из Услуги Цикл
				
				Если Не ЗначениеЗаполнено(РаботыСпециалиста) Тогда
					ОбщегоНазначения.СообщитьПользователю("Проведение невозможно. Не заполнено значение константы ""Работы специалиста""");
					Отказ = Истина;
					Продолжить;
				КонецЕсли;
				
				Если ТекСтрокаУслуги.Номенклатура = РаботыСпециалиста Тогда
					Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.ДобавитьРасход();
					Движение.Период = Дата;
					Движение.Клиент = Контрагент;
					Движение.Договор = Договор;
					Движение.СуммаКОплате = ТекСтрокаУслуги.Сумма;
					Движение.КоличествоЧасов = ТекСтрокаУслуги.Количество;
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			Движение = Движения.ОбработкаЗаказов.Добавить();
			Движение.Период = Дата;
			Движение.Контрагент = Контрагент;
			Движение.Договор = Договор;
			Движение.Заказ = Основание;
			Движение.СуммаОтгрузки = СуммаДокумента;

			Для Каждого ТекСтрокаТовары Из Товары Цикл
				Движение = Движения.ОстаткиТоваров.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Период = Дата;
				Движение.Контрагент = Контрагент;
				Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
				Движение.Сумма = ТекСтрокаТовары.Сумма;
				Движение.Количество = ТекСтрокаТовары.Количество;
			КонецЦикла;
			
		КонецЕсли;
		// -- Колесников А.А. ВКМ 30.01.26
		
	КонецЦикла;
	
КонецПроцедуры


Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// ++ Колесников А.А. ВКМ 30.01.26
	// Удаление реквизита Основание из проверяемых, если выбран Договор на абонентсоке обслуживание
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Договор.ВидДоговора КАК ВидДоговора
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|ГДЕ
		|	РеализацияТоваровУслуг.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание Тогда
			РеквизитОснование = ПроверяемыеРеквизиты.Найти("Основание");
			ПроверяемыеРеквизиты.Удалить(РеквизитОснование);
		КонецЕсли;
		
	КонецЦикла;
	// -- Колесников А.А. ВКМ 30.01.26
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказПокупателя.Организация КАК Организация,
	               |	ЗаказПокупателя.Контрагент КАК Контрагент,
	               |	ЗаказПокупателя.Договор КАК Договор,
	               |	ЗаказПокупателя.СуммаДокумента КАК СуммаДокумента,
	               |	ЗаказПокупателя.Товары.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Товары,
	               |	ЗаказПокупателя.Услуги.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Услуги
	               |ИЗ
	               |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	               |ГДЕ
	               |	ЗаказПокупателя.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	ТоварыОснования = Выборка.Товары.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Товары.Добавить(), ТоварыОснования);
	КонецЦикла;
	
	УслугиОснования = Выборка.Услуги.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Услуги.Добавить(), УслугиОснования);
	КонецЦикла;
	
	Основание = ДанныеЗаполнения;
	
КонецПроцедуры

Процедура ВыполнитьАвтозаполнение() Экспорт
	
	// ++ Колесников А.А. ВКМ 30.01.26
	// Автоматическое заполнение табличной части документа данными об абоненсткой плате и выполенных работах, 
	// если выбран договор на Абонентское обслуживание
	АбонентскаяПлата = Константы.ВКМ_НоменклатураАбонентскаяПлата.Получить();
	РаботыСпециалиста = Константы.ВКМ_НоменклатураРаботыСпециалиста.Получить();
	
	Если Не ЗначениеЗаполнено(АбонентскаяПлата) ИЛИ Не ЗначениеЗаполнено(РаботыСпециалиста) Тогда
		ОбщегоНазначения.СообщитьПользователю("Автозаполнение невозможно. Не заполнено значение констант");
		Возврат;
	КонецЕсли;
	
	Услуги.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.ВКМ_СуммаАбонентскойПлаты КАК СуммаАбонентскойПлаты,
		|	ВКМ_ВыполненныеКлиентуРаботыОбороты.СуммаКОплатеОборот КАК СуммаКОплате,
		|	ВКМ_ВыполненныеКлиентуРаботыОбороты.КоличествоЧасовОборот КАК КоличествоЧасов
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВыполненныеКлиентуРаботы.Обороты(&НачалоПериода, &КонецПериода, Месяц,) КАК
		|			ВКМ_ВыполненныеКлиентуРаботыОбороты
		|		ПО ДоговорыКонтрагентов.Ссылка = ВКМ_ВыполненныеКлиентуРаботыОбороты.Договор
		|ГДЕ
		|	ДоговорыКонтрагентов.Ссылка = &Договор
		|	И &Дата МЕЖДУ ДоговорыКонтрагентов.ВКМ_ДатаНачалаДоговора И ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДоговора";
	
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если ЗначениеЗаполнено(Выборка.СуммаАбонентскойПлаты) И Выборка.СуммаАбонентскойПлаты <> 0 Тогда 
			НоваяСтрока = Услуги.Добавить();
			НоваяСтрока.Номенклатура = АбонентскаяПлата;
			НоваяСтрока.Сумма = Выборка.СуммаАбонентскойПлаты;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.СуммаКОплате) ИЛИ ЗначениеЗаполнено(Выборка.КоличествоЧасов) Тогда
			НоваяСтрока = Услуги.Добавить();
			НоваяСтрока.Номенклатура = РаботыСпециалиста;
			НоваяСтрока.Количество = Выборка.КоличествоЧасов;
			НоваяСтрока.Сумма = Выборка.СуммаКОплате;
		КонецЕсли;
		
	КонецЕсли;
	// -- Колесников А.А. ВКМ 30.01.26
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
